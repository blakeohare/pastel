struct NumAnalysis {
	int count;
	int min;
	int max;
	int total;
	double mean;
	double median;
	double std_dev;
}

int NumAnalysis_getCount(NumAnalysis na) { return na.count; }
int NumAnalysis_getMin(NumAnalysis na) { return na.min; }
int NumAnalysis_getMax(NumAnalysis na) { return na.max; }
int NumAnalysis_getTotal(NumAnalysis na) { return na.total; }
double NumAnalysis_getMean(NumAnalysis na) { return na.mean; }
double NumAnalysis_getMedian(NumAnalysis na) { return na.median; }
double NumAnalysis_getStdDev(NumAnalysis na) { return na.std_dev; }

NumAnalysis perform_analysis(Array<int> nums, int length) {
	NumAnalysis output = new NumAnalysis(0, 0, 0, 0, 0.0, 0.0, 0.0);
	output.count = length;
	if (length > 0) {
		output.min = nums[0];
		output.max = nums[0];
		output.total = 0;
		for (int i = 0; i < length; i += 1) {
			int value = nums[i];
			output.total += value;
			if (value < output.min) output.min = value;
			if (value > output.max) output.max = value;
		}
		output.mean = Core.FloatDivision(1.0 * output.total, length);
		output.std_dev = calculate_standard_deviation(nums, length, output.mean);

		Array<int> nums_copy = Core.SortedCopyOfIntArray(nums);
		if (length % 2 == 0) {
			output.median = Core.FloatDivision(nums_copy[Core.IntegerDivision(length, 2) - 1] + nums_copy[Core.IntegerDivision(length, 2)], 2.0);
		} else {
			output.median = 0.0 + nums_copy[Core.IntegerDivision(length, 2)];
		}
	}
	return output;
}

double calculate_standard_deviation(Array<int> nums, int length, double mean) {
	double total_dev = 0.0;
	for (int i = 0; i < length; i += 1) {
		double diff = nums[i] - mean;
		total_dev += Core.Pow(diff, 2);
	}
	return Core.Pow(Core.FloatDivision(total_dev, length), 0.5);
}
