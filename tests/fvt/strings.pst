@import("fvt-assert.pst");

void runner() {

    string s = ""
        + "abc XYZ 123 .?" // ASCII
        + " ëËæÆçÇìÌ" // accents
        + " あ ア 阿 아" // CJK

        // TODO: behavior for cyrillic is different in various target languages
        // + " А" // Cyrillic
        ;

    assertEqStr("ABC XYZ 123 .? ËËÆÆÇÇÌÌ あ ア 阿 아", s.ToUpper());
    assertEqStr("abc xyz 123 .? ëëææççìì あ ア 阿 아", s.ToLower());

    // Trim
    string FW_SPACE = ""; // "　";
    string padding = " \r\n\t" + FW_SPACE;
    string testStr = padding + "." + padding;
    assertEqStr(
        readableWrap("."),
        readableWrap(testStr.Trim()));
    assertEqStr(
        readableWrap("." + padding),
        readableWrap(testStr.TrimStart()));
    assertEqStr(
        readableWrap(padding + "."),
        readableWrap(testStr.TrimEnd()));

    // Reverse
    assertEqStr("321 cba", "abc 123".Reverse());
    assertEqStr("おえういあ 321 cba", "abc 123 あいうえお".Reverse());

    string nums = "012三4五67八9";

    assertEqInt(10, nums.Size());

    assertEqStr("012", nums.SubString(0, 3));
    assertEqStr("012三", nums.SubString(0, 4));
    assertEqStr("三4五6", nums.SubString(3, 4));

    string haystack = "0123456";
    assertTrue(haystack.StartsWith("012"));
    assertTrue(haystack.EndsWith("456"));
    assertFalse(haystack.StartsWith("1"));
    assertFalse(haystack.EndsWith("5"));

    testCharacters();
}

void testCharacters() {
    char c = 'x';
    int charCode = Core.Ord(c);
    assertEqInt(charCode, 96 + 24);
    char cRoundTrip = Core.Chr(charCode + 1);
    assertEqChar('y', cRoundTrip);

    // TODO: implement updated op chain logic
    // assertEqStr("yz", cRoundTrip + "z");
    // assertEqStr("xy", "x" + cRoundTrip);
}

string readableWrap(string v) {
    return "[" + v + "]";
}
